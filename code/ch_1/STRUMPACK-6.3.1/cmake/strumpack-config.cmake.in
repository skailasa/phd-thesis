cmake_minimum_required(VERSION 3.17 FATAL_ERROR)

get_filename_component(STRUMPACK_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
list(INSERT CMAKE_MODULE_PATH 0 ${STRUMPACK_CMAKE_DIR})
list(APPEND CMAKE_PREFIX_PATH "@CMAKE_PREFIX_PATH@")

include(CMakeFindDependencyMacro)

enable_language(C)
enable_language(CXX)

if(@STRUMPACK_USE_CUDA@) # STRUMPACK_USE_CUDA
  enable_language(CUDA)
  find_dependency(CUDAToolkit)
endif()

if(@STRUMPACK_USE_OPENMP@) # STRUMPACK_USE_OPENMP
  find_dependency(OpenMP)
endif()

if(@STRUMPACK_USE_MPI@) # STRUMPACK_USE_MPI
  enable_language(Fortran)
  find_dependency(MPI)

   # there is no target for ScaLAPACK
   # find_dependency(SCALAPACK)
endif()

# don't really need to find these because they have no targets
# find_dependency(BLAS)
# find_dependency(LAPACK)

set(metis_PREFIX @TPL_METIS_PREFIX@)
set(metis_INCLUDE_DIR @TPL_METIS_INCLUDE_DIRS@)
set(metis_LIBRARY_DIR @TPL_METIS_LIBRARY_DIR@)
set(metis_LIBRARIES @TPL_METIS_LIBRARIES@)
find_dependency(METIS)

if(@STRUMPACK_USE_SCOTCH@) # STRUMPACK_USE_SCOTCH
  set(scotch_PREFIX @TPL_SCOTCH_PREFIX@)
  set(scotch_INCLUDE_DIR @TPL_SCOTCH_INCLUDE_DIRS@)
  set(scotch_LIBRARY_DIR @TPL_SCOTCH_LIBRARY_DIR@)
  set(scotch_LIBRARIES @TPL_SCOTCH_LIBRARIES@)
  find_dependency(SCOTCH)
endif()

if(@STRUMPACK_USE_PTSCOTCH@) # STRUMPACK_USE_PTSCOTCH
  set(ptscotch_PREFIX @TPL_PTSCOTCH_PREFIX@)
  set(ptscotch_INCLUDE_DIR @TPL_PTSCOTCH_INCLUDE_DIRS@)
  set(ptscotch_LIBRARY_DIR @TPL_PTSCOTCH_LIBRARY_DIR@)
  set(ptscotch_LIBRARIES @TPL_PTSCOTCH_LIBRARIES@)
  find_dependency(PTSCOTCH)
endif()

if(@STRUMPACK_USE_PARMETIS@) # STRUMPACK_USE_PARMETIS
  set(parmetis_PREFIX @TPL_PARMETIS_PREFIX@)
  set(parmetis_INCLUDE_DIR @TPL_PARMETIS_INCLUDE_DIRS@)
  set(parmetis_LIBRARY_DIR @TPL_PARMETIS_LIBRARY_DIR@)
  set(parmetis_LIBRARIES @TPL_PARMETIS_LIBRARIES@)
  find_dependency(ParMETIS)
endif()

if(@STRUMPACK_USE_ZFP@) # STRUMPACK_USE_ZFP
  set(zfp_PREFIX @TPL_ZFP_PREFIX@)
  set(zfp_INCLUDE_DIR @TPL_ZFP_INCLUDE_DIRS@)
  set(zfp_LIBRARY_DIR @TPL_ZFP_LIBRARY_DIR@)
  set(zfp_LIBRARIES @TPL_ZFP_LIBRARIES@)
  find_dependency(ZFP)
endif()

if(@STRUMPACK_USE_BPACK@) # STRUMPACK_USE_BPACK
  find_dependency(ButterflyPACK)
endif()

if(@STRUMPACK_USE_SLATE@) # STRUMPACK_USE_SLATE
  set(blaspp_DIR @blaspp_DIR@)
  set(lapackpp_DIR @lapackpp_DIR@)
  set(slate_DIR @slate_DIR@)
  find_dependency(blaspp)
  find_dependency(lapackpp)
  find_dependency(slate)
endif()

list(REMOVE_AT CMAKE_MODULE_PATH 0)

if(NOT TARGET STRUMPACK::strumpack)
  include("${CMAKE_CURRENT_LIST_DIR}/strumpack-targets.cmake")
endif()

set(STRUMPACK_LIBRARIES STRUMPACK::strumpack)
